apply plugin: 'com.android.application'

def internalCommonDataPath = "../../Android/app/src/main/assets/data"
def externalCommonDataPath = "../../Common/Data"
def externalDataMakeFile = "../../Android-NDK/CMakeLists.txt"

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    //sourceCompatibility = 1.8

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def SEP = "_"
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def version = variant.versionName
            def versionCode = variant.versionCode
            outputFileName = "internetmap${SEP}${buildType}${SEP}${version}${SEP}${versionCode}.apk"
        }
    }

    defaultConfig {
        applicationId "com.peer1.internetmap"
        minSdkVersion 21 // Currently having issues with 19 and pthreads
        targetSdkVersion 27

        versionCode 19
        versionName "2.1.0"

        // Not sure this is required, commenting out.
//        compileOptions {
//            sourceCompatibility JavaVersion.VERSION_1_5
//            targetCompatibility JavaVersion.VERSION_1_5
//        }

        //APP_ABI := armeabi x86

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
                arguments "-DBoost_DEBUG=ON"
            }
        }

        // Suggested to get around issue where NDK-18 was giving
        // "No toolchains found in the NDK toolchains folder for ABI with prefix: mips64el-linux-android"
        // error.
        // Does not appear to work, but keep to trouble shoot later.
//        packagingOptions{
//            doNotStrip '*/mips/*.so'
//            doNotStrip '*/mips64/*.so'
//        }

        ndk {
            moduleName "internetmaprenderer"
            // Also added to fix toolchain issue, did not work.
//            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86' // <- only the supported ones
        }
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    externalNativeBuild {
        cmake {
            path externalDataMakeFile
        }
    }
}

task deleteCommonData(type: Delete) {
    description = 'Deleting common Data files'
    delete fileTree(internalCommonDataPath) {
        include '*.txt'
        include '*.json'
        include '*.html'
    }
}

task copyCommonData(type: Copy) {
    description = 'Copying common Data files'
    from externalCommonDataPath
    into internalCommonDataPath
    include '*.txt'
    include '*.json'
    include '*.html'
}

task synchCommonDataFiles {
    // Copying files directly is creating a relative link issue with the ExternalFiles folder
    dependsOn 'deleteCommonData'
    dependsOn 'copyCommonData'
    tasks.findByName('copyCommonData').mustRunAfter 'deleteCommonData'
}

// Copy over files from Common folder
preBuild.dependsOn(synchCommonDataFiles)

dependencies {
    compile 'com.android.support:support-v4:27.0.2'
    compile 'com.android.support:design:27.0.2'

    compile files('libs/android-async-http-1.4.2.jar')

    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'

    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'uk.co.chrisjenx:calligraphy:2.3.0'
    compile 'com.github.spyhunter99:super-tooltip-aar:4.0.0'
}

repositories {
    google()
}
buildscript {
    repositories {
        google()
    }
}